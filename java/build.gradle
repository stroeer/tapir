plugins {
  id 'idea'
  id 'java-library'
  id 'maven-publish'
}

ext {
  apiVersion = 'v1'
  externalVersion = System.getenv("RELEASE_TAG") ?: "0.0.1-SNAPSHOT"

  gh_username = System.getenv("GITHUB_ACTOR") ?: (project.findProperty('github.actor') ?: "none")
  gh_token = System.getenv("GITHUB_TOKEN") ?: (project.findProperty('github.token') ?: "none")
}

group = 'de.stroeer.api.grpc'
archivesBaseName = "${project.name}-${apiVersion}"
version = externalVersion
sourceCompatibility = '11'

repositories {
  mavenCentral()
}

configurations {
  genGrpc
}

dependencies {
  implementation(
    'io.grpc:grpc-services:1.29.0',
    'javax.annotation:javax.annotation-api:1.3.2'
  )

  testImplementation(
    'org.junit.jupiter:junit-jupiter:5.6.2'
  )

  genGrpc(
    group: 'io.grpc', name: 'protoc-gen-grpc-java', version: '1.32.1', classifier:'linux-x86_64', ext: 'exe' 
  )
}

task printVersion {
  doLast {
    println "publishing: ${project.group}:${archivesBaseName}:${version}"
  }
}
publishToMavenLocal.dependsOn printVersion

task download(type: Copy, group: 'help', description: 'Download binaries required for GRPC generation') {
  from configurations.genGrpc
  into 'installer'
  rename { file ->
    file.replace(file, 'protoc-gen-grpc-java')
  }
}

java {
  withSourcesJar()
}

test {
  useJUnitPlatform()
}

publishing {
  repositories {
    maven {
      name = 'GitHubPackages'
      url = uri('https://maven.pkg.github.com/stroeer/tapir')
      credentials {
        username = gh_username
        password = gh_token
      }
    }
  }

  publications {
    gpr(MavenPublication) {
      from(components.java)

      artifactId "${archivesBaseName}"
      pom {
        name = artifactId
        description = "T-Online GRPC API for ${artifactId}"
        url = 'https://github.com/stroeer/tapir'

        organization {
          name = 'T-Online'
          url = 'https://www.t-online.de'
        }

        scm {
          url = 'https://github.com/stroeer/tapir'
        }
      }
    }
  }
}
