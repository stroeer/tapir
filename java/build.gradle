plugins {
  id 'idea'
  id 'java-library'
  id 'maven-publish'
  id 'com.github.ben-manes.versions' version '0.39.0'
}

ext {
  apiVersion = 'v1'
  gh_username = System.getenv("GITHUB_ACTOR") ?: (project.findProperty('github.actor') ?: "none")
  gh_token = System.getenv("GITHUB_TOKEN") ?: (project.findProperty('github.token') ?: "none")
}

group = 'de.stroeer.api.grpc'
archivesBaseName = "${project.name}-${apiVersion}"
sourceCompatibility = '11'

repositories {
  mavenCentral()
}

configurations {
  genGrpc
}

dependencies {
  def grpc = '1.37.0'

  implementation group: 'io.grpc', name: 'grpc-services', version: grpc
  implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.1'

  genGrpc group: 'io.grpc', name: 'protoc-gen-grpc-java', version: grpc, classifier: 'linux-x86_64', ext: 'exe'
}

task printVersion {
  doLast {
    println "publishing: ${project.group}:${archivesBaseName}:${version}"
  }
}
publishToMavenLocal.dependsOn printVersion

task download(type: Copy, group: 'help', description: 'Download binaries required for GRPC generation') {
  from configurations.genGrpc
  into 'installer'
  rename { file ->
    file.replace(file, 'protoc-gen-grpc-java')
  }
}

java {
  withSourcesJar()
}

tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
}

test {
  useJUnitPlatform()
}

publishing {
  repositories {
    maven {
      name = 'GitHubPackages'
      url = uri('https://maven.pkg.github.com/stroeer/tapir')
      credentials {
        username = gh_username
        password = gh_token
      }
    }
  }

  publications {
    gpr(MavenPublication) {
      from(components.java)

      artifactId "${archivesBaseName}"
      pom {
        name = artifactId
        description = "T-Online GRPC API for ${artifactId}"
        url = 'https://github.com/stroeer/tapir'

        organization {
          name = 'T-Online'
          url = 'https://www.t-online.de'
        }

        scm {
          url = 'https://github.com/stroeer/tapir'
        }
      }
    }
  }
}
