plugins {
    id 'idea'
    id 'java'
    id "maven-publish"

}

ext {
    externalVersion = System.getenv("RELEASE_TAG") ?: "v0.0.local"
    gh_username = System.getenv("GITHUB_USERNAME") ?: "username"
    gh_token = System.getenv("GITHUB_TOKEN") ?: "github_token"
}

group = 'de.stroeer'
version = externalVersion
sourceCompatibility = '11'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation(
            'io.grpc:grpc-services:1.28.1',
            'javax.annotation:javax.annotation-api:1.3.2',
            'org.reflections:reflections:0.9.12',
            'com.fasterxml.jackson.core:jackson-databind:2.11.0',
            'org.springframework:spring-context:5.2.6.RELEASE'
    )
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
        }
    }
    test {
        java {
            srcDirs 'src/test/java'
        }
    }
}


test {
    useJUnitPlatform()
}

task printVersion {
    doLast {
        println "Version: ${project.getGroup()}:${project.getName()}:${project.getVersion()}"
    }
}
publishToMavenLocal.dependsOn printVersion

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/stroeer/tapir")
            credentials {
                username = gh_username
                password = gh_token
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}