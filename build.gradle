plugins {
    id 'idea'
    id 'java'
    id "com.google.protobuf" version "0.8.12"
    id "maven-publish"

}

group = 'de.stroeer'
version = System.getenv("RELEASE_TAG") ?: "v0.0.local"
sourceCompatibility = '11'

task version(type: VersionTask)
class VersionTask extends DefaultTask {
    @TaskAction
    def version() {
        println project.version
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation(
            'com.google.protobuf:protobuf-java:3.11.4',
            'io.grpc:grpc-services:1.28.1',
            'javax.annotation:javax.annotation-api:1.3.2'
    )
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
        proto {
            srcDir '../api'
        }
    }
}


protobuf {
    protoc {
        path = "bin/protoc"
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.28.1'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

java {
//    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/stroeer/tapir")
            credentials {
                username = System.getenv("GITHUB_USERNAME") ?: "username"
                password = System.getenv("GITHUB_TOKEN") ?: "github_token"
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}