plugins {
    id 'idea'
    id 'java'
    id "com.google.protobuf" version "0.8.12"
    id "maven-publish"

}

group = 'de.stroeer'
version = System.getenv("RELEASE_TAG") ?: "v0.0.local"
sourceCompatibility = '11'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation(
            'com.google.protobuf:protobuf-java:3.11.4',
            'io.grpc:grpc-services:1.28.1',
            'javax.annotation:javax.annotation-api:1.3.2'
    )
}

sourceSets {
    main {
        java {
            srcDirs 'generated/main/java'
            srcDirs 'generated/main/grpc'
        }
        proto {
            srcDir 'api'
        }
    }
}


protobuf {
    generatedFilesBaseDir = "$projectDir/generated"

    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.28.1'
        }
        ts {
            path = './node_modules/ts-protoc-gen/bin/protoc-gen-ts'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            ts {
                option 'import_style=commonjs'
                option 'binary'
            }
            grpc {}
        }
    }
}

java {
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/stroeer/tapir")
            credentials {
                username = System.getenv("GITHUB_USERNAME") ?: "username"
                password = System.getenv("GITHUB_TOKEN") ?: "github_token"
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}